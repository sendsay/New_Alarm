#include <Arduino.h>
#include <SoftwareSerial.h>


/*
..#######..########........##..........##....##.....##....###....########.
.##.....##.##.....##.......##.........##.....##.....##...##.##...##.....##
.##.....##.##.....##.......##........##......##.....##..##...##..##.....##
.##.....##.########........##.......##.......##.....##.##.....##.########.
.##.....##.##.....##.##....##......##.........##...##..#########.##...##..
.##.....##.##.....##.##....##.....##...........##.##...##.....##.##....##.
..#######..########...######.....##.............###....##.....##.##.....##
*/

SoftwareSerial SIM800(2, 3);                // Setup SIM800
String _response = "";                      // Answer data from SIM800


 /*
 .########.##.....##.##....##..######..########.####..#######..##....##..######.
 .##.......##.....##.###...##.##....##....##.....##..##.....##.###...##.##....##
 .##.......##.....##.####..##.##..........##.....##..##.....##.####..##.##......
 .######...##.....##.##.##.##.##..........##.....##..##.....##.##.##.##..######.
 .##.......##.....##.##..####.##..........##.....##..##.....##.##..####.......##
 .##.......##.....##.##...###.##....##....##.....##..##.....##.##...###.##....##
 .##........#######..##....##..######.....##....####..#######..##....##..######.
 */

String waitResponse() {                         // Функция ожидания ответа и возврата полученного результата
  String _resp = "";                            // Переменная для хранения результата
  long _timeout = millis() + 10000;             // Переменная для отслеживания таймаута (10 секунд)
  while (!SIM800.available() && millis() < _timeout)  {}; // Ждем ответа 10 секунд, если пришел ответ или наступил таймаут, то...
  if (SIM800.available()) {                     // Если есть, что считывать...
    _resp = SIM800.readString();                // ... считываем и запоминаем
  }
  else {                                        // Если пришел таймаут, то...
    Serial.println("Timeout...");               // ... оповещаем об этом и...
  }
  _resp.trim();
  return _resp + "\n";                                 // ... возвращаем результат. Пусто, если проблема
}

String sendATCommand(String cmd, bool waiting) {
  String _resp = "";                            // Переменная для хранения результата
  Serial.println(cmd);                          // Дублируем команду в монитор порта
  SIM800.println(cmd);                          // Отправляем команду модулю
  if (waiting) {                                // Если необходимо дождаться ответа...
    _resp = waitResponse();                     // ... ждем, когда будет передан ответ
    Serial.println(_resp);                      // Дублируем ответ в монитор порта
  }
  _resp.trim();
  return _resp + "\n";                                 // Возвращаем результат. Пусто, если проблема
}


/*
..######..########.########.##.....##.########.
.##....##.##..........##....##.....##.##.....##
.##.......##..........##....##.....##.##.....##
..######..######......##....##.....##.########.
.......##.##..........##....##.....##.##.......
.##....##.##..........##....##.....##.##.......
..######..########....##.....#######..##.......
*/

void setup() {
  Serial.begin(9600);                       
  SIM800.begin(9600);                       
  Serial.println("Begin!");

  sendATCommand("ATE0", true);              // Echo off
 
  sendATCommand("AT", true);                // Adjust speed
  sendATCommand("AT+CMGF=1", true);         // Text Mode sms
  sendATCommand("AT+DDET=1,0,1", true);     // DTMF on
  sendATCommand("AT+COLP=1", true);         // Line identify   

  sendATCommand("AT+CLIP=1", true);         // Caller ID 

  Serial.println("Begin");     
}


/*
.##........#######...#######..########.
.##.......##.....##.##.....##.##.....##
.##.......##.....##.##.....##.##.....##
.##.......##.....##.##.....##.########.
.##.......##.....##.##.....##.##.......
.##.......##.....##.##.....##.##.......
.########..#######...#######..##.......
*/

void loop() {
  
   if (SIM800.available())   {                   // Если модем, что-то отправил...
    _response = waitResponse();                 // Получаем ответ от модема для анализа
    _response.trim();                           // Убираем лишние пробелы в начале и конце
    Serial.println(_response);                  // Если нужно выводим в монитор порта
    String whiteListPhones = "+37062460"; // Белый список телефонов
    if (_response.startsWith("RING")) {         // Есть входящий вызов
      int phoneindex = _response.indexOf("+CLIP: \""); // Есть ли информация об определении номера, если да, то phoneindex>-1
      String innerPhone = "";                   // Переменная для хранения определенного номера
      if (phoneindex >= 0) {                    // Если информация была найдена
        phoneindex += 8;                        // Парсим строку и ...
        innerPhone = _response.substring(phoneindex, _response.indexOf("\"", phoneindex)); // ...получаем номер
        Serial.println("Number: " + innerPhone); // Выводим номер в монитор порта
      }
      // Проверяем, чтобы длина номера была больше 6 цифр, и номер должен быть в списке
      if (innerPhone.length() >= 7 && whiteListPhones.indexOf(innerPhone) >= 0) {
        sendATCommand("ATA", true);        // Если да, то отвечаем на вызов
      }
      else {
        sendATCommand("ATH", true);        // Если нет, то отклоняем вызов
      }
    }
  }
  if (Serial.available())  {                    // Ожидаем команды по Serial...
    SIM800.write(Serial.read());                // ...и отправляем полученную команду модему
  };
}

/*
.########.##....##.########.
.##.......###...##.##.....##
.##.......####..##.##.....##
.######...##.##.##.##.....##
.##.......##..####.##.....##
.##.......##...###.##.....##
.########.##....##.########.
*/